@name EGP Menu
#Windows 10.4 update 0
@persist ContextMenu:string

#Cost 3 egpID
function number appChoiceMenu(EGPID, Pos:vector2, Color:vector, VarName:string, ChooseText:array) {
    local EGP = EGP + EGPID
    if (!Screen:egpHasObject(EGP) & egpCanSendUmsg()) {
        appBox(EGPID, Pos + vec2(40, 7.5 + appN(VarName + "List") * 15), vec2(80, 15), Color)
        
        local ChooseText1 = array()
        local ChooseText2 = array()
        foreach(I, N:string = ChooseText) {
            if (I % 2) {
                ChooseText1:pushString(N)
            }
            else {
                ChooseText2:pushString(N)
            }
        }
        appTextUni(EGPID + 1, ChooseText1:concat("\n\n\n"):explode("\n"), Pos + vec2(3, 2.5))
        #appTextUni(EGPID + 2, ChooseText2:concat("\n\n\n"):explode("\n"), Pos + vec2(3, 17.5), 10)

        Screen:egpCopy(EGP + 2, EGP + 1)
        Screen:egpPos(EGP + 2, Pos + vec2(3, 17.5))
        Screen:egpSetText(EGP + 2, ChooseText2:concat("\n\n\n"))

        if (appS(VarName) != ChooseText[appN(VarName + "List"), string]) {
            foreach(I, N:string = ChooseText) {
                if (appS(VarName) == N) {
                    appN(VarName + "List", I - 1)
                    Screen:egpPos(EGP, Pos + vec2(40, 7.5 + appN(VarName + "List") * 15))
                    break
                }
            }
        }
        if (!appS(VarName)) {
            appS(VarName, ChooseText[appN(VarName + "List") + 1, string])
        }
        return 1
    }
    elseif(appEgpInrange(Pos, Pos + vec2(80, ChooseText:count() * 15)) & App_Mouse) {
        appN(VarName + "List", appChoiceHelper(Pos, 15):y())
        Screen:egpPos(EGP, Pos + vec2(40, 7.5 + appN(VarName + "List") * 15))
        appS(VarName, ChooseText[appN(VarName + "List") + 1, string])
        return changed(appN(VarName + "List"))
    }
    return 0
}

function number appChoiceMenu(EGPID, Pos:vector2, VarName:string, ChooseText:array) {
    return appChoiceMenu(EGPID, Pos, ROM["System", table]["Theme", vector], VarName, ChooseText)
}

#Cost 3 egpID from end
function number appContextMenu(VarName:string, Reason, Chooses:array) {
    local EGP = EGPID_App["Free", array][1, vector2][2]
    local Size = vec2(80, Chooses:count() * 10)
    if (Reason & !Screen:egpHasObject(EGP) & egpCanSendUmsg()) {
        ContextMenu = App_This + " " + VarName
        Screen:egpBox(EGP, Sys_CursorPos + vec2(3) + Size / 2, Size + vec2(6)) Screen:egpColor(EGP, vec(245)) Screen:egpParent(EGP, 1)
        egpAdvBox(EGP - 1, EGP, vec2(), vec2(80, 10), ROM["System", table]["Theme", vector])
        egpTextBlack(EGP - 2, EGP, Chooses, vec2(3, 0) - Size / 2, 10)
        egpAdvBoxOutline(EGP - 3, EGP, vec2(), Size + vec2(6), vec(215))
    }
    elseif(ContextMenu == (App_This + " " + VarName)) {
        if (inrange(Sys_CursorPos, Screen:egpPos(EGP) - Size / 2, Screen:egpPos(EGP) + Size / 2)) {
            Screen:egpPos(EGP - 1, vec2(0, egpChoiceHelper(Screen:egpPos(EGP) - Size / 2, 10):y() * 10 - Size:y() / 2 + 5)) Screen:egpAlpha(EGP - 1, 255)
            if (App_Click) {
                local Choice = egpChoiceHelper(Screen:egpPos(EGP) - Size / 2, 10):y() + 1
                appS(VarName, Chooses[Choice, string])
            }
        }
        else {
            Screen:egpAlpha(EGP - 1, 0)
        }
        if (App_Click) {
            for (I = EGP - 3, EGP) {
                Screen:egpRemove(I)
            }
            ContextMenu = ""
        }
    }
    elseif(App_Click) {
        for (I = EGP - 3, EGP) {
            Screen:egpRemove(I)
        }
        ContextMenu = ""
    }
    return Screen:egpHasObject(EGP)
}

function string appContextMenuAnswear(VarName:string) {
    local Return = appS(VarName + "Action")
    if (Return != "") {
        appS(VarName + "Action", "")
        return Return
    }
    return ""
}
